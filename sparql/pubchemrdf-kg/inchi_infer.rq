## DESCRIPTION
##
## Finds the InChI for a given compound name (?name).
##
## @param ?name compound name
##
## @return
##
##  ?compound [IRI]     PubChem Compound ID (CID)
##  ?inchi    [literal] InChI descriptor
##
## Uses db:Virtuoso extension for enabling inference.
##
## @graphdb db:Virtuoso

# Enables inference in db:Virtuoso.
DEFINE input:inference 'urn:ontology'

PREFIX rdfs:       <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sio:        <http://semanticscience.org/resource/>
PREFIX cheminf:    <http://semanticscience.org/resource/>
PREFIX pubchem:    <http://rdf.ncbi.nlm.nih.gov/pubchem/>
PREFIX pccompound: <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>
PREFIX pcsynonym:  <http://rdf.ncbi.nlm.nih.gov/pubchem/synonym/>

SELECT
  ?compound
  ?name
  ?inchi
WHERE {
  # INPUT
  BIND( 'aspirin' AS ?name )

  # InChI descriptor
  BIND( cheminf:CHEMINF_000113 AS ?cls_cheminf_inchi )

  GRAPH pubchem:synonym {
    ?synonym sio:SIO_000300 ?name     ;
             sio:SIO_000011 ?compound .
  }

  ?compound sio:SIO_000008 [
    a              ?cls_cheminf_inchi ;
    sio:SIO_000300 ?inchi             ;
  ].

  FILTER( STRSTARTS(STR(?compound), STR(pccompound:)) )
}
LIMIT 1
