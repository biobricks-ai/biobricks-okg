## DESCRIPTION
##
## Finds the InChIKey for a given compound by PubChem compound CID.
##
## @param ?name compound name
##
## @return
##
##  ?compound [IRI]     PubChem Compound ID (CID)
##  ?inchiKey [literal] InChIKey identifier
##
## Uses db:Virtuoso extension for enabling inference.
##
## @graphdb db:Virtuoso

# Enables inference in db:Virtuoso
#
# isql:
#
# ```sql
# SPARQL LOAD <http://semanticscience.org/ontology/sio.owl> ;
# rdfs_rule_set( 'urn:ontology', 'http://semanticscience.org/ontology/sio.owl' ) ;
# ```
DEFINE input:inference 'urn:ontology'

PREFIX rdfs:       <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sio:        <http://semanticscience.org/resource/>
PREFIX cheminf:    <http://semanticscience.org/resource/>
PREFIX pubchem:    <http://rdf.ncbi.nlm.nih.gov/pubchem/>
PREFIX pccompound: <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>
PREFIX pcsynonym:  <http://rdf.ncbi.nlm.nih.gov/pubchem/synonym/>

SELECT
  ?compound
  ?name
  ?inchiKey
WHERE {
  # INPUT
  BIND( 'aspirin' AS ?name )

  # InChIKey identifier
  BIND( cheminf:CHEMINF_000059 AS ?cls_cheminf_inchiKey )

  GRAPH pubchem:synonym {
    ?synonym sio:SIO_000300 ?name     ;
             sio:SIO_000011 ?compound .
  }

  ?compound sio:SIO_000008 [
    a              ?cls_cheminf_inchiKey ;
    sio:SIO_000300 ?inchiKey             ;
  ].

  FILTER( STRSTARTS(STR(?compound), STR(pccompound:)) )
}
LIMIT 1
