## DESCRIPTION:
##
## Look up the names of chemical entities and optionally their CAS RN and
## DSSTOX SID if available. Indicate which graph the data come from.
##
## Does not remove the ?file= suffix in the query like <report/2024-01-15/chemical-name-id.rq> does.
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX CHEMINF: <http://purl.obolibrary.org/obo/CHEMINF_>
PREFIX CAS:     <http://identifiers.org/cas/>
PREFIX EDAM:    <http://edamontology.org/>
PREFIX dce:     <http://purl.org/dc/elements/1.1/>
PREFIX BAO:     <http://www.bioassayontology.org/bao#BAO_>

SELECT
    ?chem ?dsstox ?cas
    ( GROUP_CONCAT(?graph;separator=", ") AS ?graphs  )
WHERE {
    graph ?graph {
      ?chem a          CHEMINF:000000;
            rdfs:label ?name .
      OPTIONAL {
	?chem   EDAM:has_identifier ?dsstox.
	?dsstox a                   CHEMINF:000568. # DSSTOX SID
      }
      OPTIONAL {
	?chem EDAM:has_identifier ?cas.
	?cas  a                   CHEMINF:000446.   # CAS RN
      }
    }
}
GROUP BY ?chem ?dsstox ?cas
ORDER BY ?chem
LIMIT 200000
